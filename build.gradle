plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'net.kakoen.arksa'
version = '0.0.19'

repositories {
    mavenCentral()
}

java {
    withSourcesJar()

    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}


dependencies {
    implementation group: 'org.slf4j', name: 'slf4j-simple', version: '2.0.12'

    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.30'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.30'

    testCompileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.30'
    testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.30'

    implementation group: 'org.xerial', name: 'sqlite-jdbc', version: '3.45.2.0'

    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.16.1'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

test {
    useJUnitPlatform()
}

//jar {
//    finalizedBy shadowJar
//    manifest {
//        attributes 'Main-Class': 'net.kakoen.arksa.cli.SaveToolsCLI'
//    }
//}

shadowJar {
    from 'simplelogger.properties'
    archiveFileName.set("${project.name}.jar")
}

jar.finalizedBy(shadowJar)

/** Publish start **/
project.publishing.repositories {
    maven {
        url "https://nexus.kakoen.net/repository/maven-" + (version.contains("SNAPSHOT") ? 'snapshots' : 'releases')
    }
}

publishing.publications {
    mavenJava(MavenPublication) {
        from components.java
    }
}

project.gradle.taskGraph.whenReady {

    if(project.gradle.taskGraph.hasTask(":publish")) {
        if(!project.hasProperty('nexusUser')) {
            project.ext.nexusUser = System.console()?.readLine("\nPlease provide your username for publishing to nexus: ")
        } else {
            println "Username for nexus: ${project.nexusUser}"
        }

        if(!project.hasProperty("nexusPassword")) {
            project.ext.nexusPassword = new String(System.console()?.readPassword("\nPlease provide the password for user '${project.nexusUser}' for publishing to nexus: "))
        } else {
            println "Password for nexus: ${project.nexusPassword.replaceAll(".", "*")}"
        }

        project.publishing.repositories.maven.credentials {
            username project.nexusUser
            password project.nexusPassword
        }
    }
}
/** Publish end **/