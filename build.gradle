plugins {
    id 'java-library'
    id 'maven-publish'
}

group = 'net.kakoen.arksa'
version = '0.1.0'

repositories {
    mavenCentral()
}

java {
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}


dependencies {
    implementation group: 'org.slf4j', name: 'slf4j-simple', version: '2.0.17'

    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.38'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.38'

    testCompileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.38'
    testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.38'

    implementation group: 'org.xerial', name: 'sqlite-jdbc', version: '3.50.1.0'

    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.19.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.13.1'
    testImplementation 'net.openhft:zero-allocation-hashing:0.27ea0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()
}

jar {
    archiveFileName.set("${project.name}.jar")
}

publishing {
    //For CI, we only want to publish when the version ends with SNAPSHOT; we don't want to overwrite releases
    def onlyPublishSnapshotVersion = rootProject.hasProperty('onlyPublishSnapshotVersion')
    if(!onlyPublishSnapshotVersion || (onlyPublishSnapshotVersion && version.endsWith("-SNAPSHOT"))) {
        publications {
            asaSaveToolsLib(MavenPublication) {
                from components.java
            }
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/kakoen/ark-sa-save-tools"
            credentials {
                username = System.getenv("GITHUB_ACTOR") ?: "github"
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}